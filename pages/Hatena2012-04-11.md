---
title: "Hatena2012-04-11"
---

hatena

```
<body>
*1334103370*Alloyガール5
ここまでの話はこちら: <a href='http://d.hatena.ne.jp/nishiohirokazu/20120410/1334024972'>Alloy</a>

** 第5回

Alloy: もしかしてStateManager.stateがStateManagerのメンバ変数だとか、State.belongがStateのメンバ変数だとか勘違いしているんじゃない？
僕: えっ、違うの？
Alloy: ぜんぜん違う。オブジェクト指向に毒されているわね。

** おさらい

Alloy: まずおさらいしてみましょう。

[f:id:nishiohirokazu:20120410111138p:image]

>||
one sig StateManager {
	state: set State
}

sig State {
}

fact {
	all s: State | s in StateManager.state
}

run {} for exactly 3 State
||<

Alloy: このとき、Stateとはなんでしょう？
JS: クラス？
僕: いや、クラスじゃなくてシグニチャだよね。
Alloy: ふーん、君はそう認識しているのか。じゃあ、シグニチャとはなんでしょう？
僕: えっ。
Alloy: それが説明できないのでは「sig = シグニチャ」っていう一欠片の知識をオウム返ししているだけね。何も理解していないのと同じだわ。
僕: うむむ、えっと、型？
Alloy: じゃあ、型とは何？
僕: うーん……、変数についたメタデータ、ってのはこの場合しっくりこないから……値の集合？
Alloy: そうね。じゃあEvaluatorで実際の値を眺めてみましょうか。

** Evaluatorを使う

Alloy: 事例を可視化したウィンドウでEvaluatorのボタンを押せば、その実例においてStateとかが具体的にどういう値になっているか観察できるわ。

[f:id:nishiohirokazu:20120411091544p:image]

Alloy: つまりStateは3つのアトムState$0, State$1, State$2から構成された集合ということね。
JS: アトム？
Alloy: アトムは分割ができず、時間によって変化せず、意味を持たないもの。
僕: ただのラベルのようなものだね。
Alloy: そう。そしてstateは関係の集合。{StateManager$0 -> State$0, StateManager$0 -> State$1, StateManager$0 -> State$2}。ここで問題です。関係とは何でしょう。はい、僕君！
僕: えっ、あっ、アトムが2個ペアになったもの？
Alloy: そうね。2個とまで言い切ってしまうと「二項関係」って前提が入っちゃってるけど。例えばこれをみて。

>||
sig X{
	rel: X -> X
}

run {
	one rel
} for exactly 2 X
||<

Alloy: これならrelは例えば{X$0 -> X$1 -> X$0}になるわ。
僕: これは3つのアトムの組だと考えていいの？
Alloy: ええ。この場合relは三項関係ね。

** 次回予告: ドットは結合

Alloy: で、あなたがJavaなどのメンバアクセス演算子と混同したドット演算子のことだけど。
僕: stateがトップレベルでアクセスできるってあたりがまず目からウロコだった。
Alloy: Alloyのドット演算子は結合で、あ、いけない今日は用事があるんだった、続きはまた今度！
僕: えー

*1334149939*Alloyでイラストロジックを解く
ラボで昼休みにAlloyの話をしていたら中谷さん(id:n_shuyo)に「Alloyでイラストロジックは解けるの？」と質問されました。で、頑張ってみた結果、結局解けなかった、という話。

イラストロジックのルールと元データに関してはこちらを参照: <a href='http://homepage1.nifty.com/sabo10/rulelog/illust.html'>ルールと解き方～イラストロジック～</a>

** 1次元イラストロジック

まず問題は簡単な方から解こうということで、1次元のイラストロジック「10個のマスがあって、ヒントは[7]」を解かせてみる。

>||
open util/ordering[Cell]

sig Cell {
	color: Color
}

enum Color {Black, White}

pred all_color(start, end : Cell, col: Color){
	all c: (start + start.nexts) & (end.prevs + end){
		c.color = col
	}
}

fact {
	some b : Cell { // block head
		let b7 = b.next.next.next.next.next.next {
			one b7
			all_color[first, b.prev, White]
			all_color[b, b7, Black]
			all_color[b7.next, last, White]
		}
	}
}
run {} for 3 but exactly 10 Cell
||<

実行してみると、正しく4通りの解があることがわかる。「10個の全順序が導入されたCellの中にあるbが存在して「bの6回nextをたどったcellが存在し、bの手前までのcellが白で、bからb7までが黒、b7の次以降が白」が満たされている」という条件で探索したわけだ。

より難しい問題に挑戦する前に挙動が変わらないか観察しながらリファクタリング。

>||
open util/ordering[Cell]

sig Cell {
	color: Color
}

enum Color {Black, White}

pred all_black(start, end : Cell, size: Int){
	let block = (start + start.nexts) & (end.prevs + end){
		#block = size
		all c: block{
			c.color = Black
		}
	}
}

pred all_white(start, end : Cell){
	let block = (start + start.nexts) & (end.prevs + end){
		all c: block{
			c.color = White
		}
	}
}

fact {
	some c: Cell {
		all_white[first, c.prev]
		some c2: Cell {
			all_black[c, c2, 7]
			all_white[c2.next, last]
		}
	}
}
run {} for 3 but exactly 10 Cell
||<

nextを連打する代わりに集合のサイズで判定するようにした。

** 1次元イラストロジック、ヒントのブロックが複数
次はヒントが[6, 3]みたいなケースを考えてみよう。少し考えて、黒の塊ごとにsomeが2つ必要だと判断。
>||
fact {
	some c: Cell {
		all_white[first, c.prev]
		some c2: c.nexts {
			all_black[c, c2, 6]
			some c3: c2.next.nexts {
				all_white[c2.next, c3.prev]
				some c4: c3.nexts{
					all_black[c3, c4, 3]
					all_white[c4.next, last]
				}
			}
		}
	}
}
||<

あるセルcが存在して、最初のセルからcの手前までは白、cより後にあるセルc.nextsの中にc2があって、cからc2までは長さ6で全部黒、c2.nextは白なのでc2.nextより後にあるc3があって、c3の手前までは白、c3より後にc4があって、c3からc4までは長さ3で全部黒、c4より後は最後まで全部白、と書いてある。

実行してみると解が正しく1個だけ見つかる。これで1次元のイラストロジックは解けるようになった。

** 10x10の2次元イラストロジックに挑戦
イラストロジックを解くためには、このコードをヒントの個数だけ生成する必要がある。あと、1次元でないということは単純な全順序ではない。それぞれのセルが「縦方向の次」と「横方向の次」を持ってる。というわけでutil/orderingを使うのを諦めた。まずは正方形に並んだ100個のCellを表現するためのコードを生成。約300行。

>||
abstract sig Cell {
  color: Color,
  hnext: lone Cell,
  vnext: lone Cell
}

(snip)

one sig Cell_0_0 extends Cell {}
one sig Cell_0_1 extends Cell {}
one sig Cell_0_2 extends Cell {}
(snip)
one sig Cell_9_7 extends Cell {}
one sig Cell_9_8 extends Cell {}
one sig Cell_9_9 extends Cell {}

fact matrix_adj {
  Cell_0_0.vnext = Cell_0_1
  Cell_0_1.vnext = Cell_0_2
  Cell_0_2.vnext = Cell_0_3
(snip)
  Cell_6_9.hnext = Cell_7_9
  Cell_7_9.hnext = Cell_8_9
  Cell_8_9.hnext = Cell_9_9
  no Cell_9_9.hnext
}
||<

こんな感じで100個の名前がついたCellと、その間の隣接関係を定義して、10x10のマトリクスを無理やり生成しました。

パズルのヒントも生成。約200行:

>||
fact {
  let next = hnext {
    some c1: from[Cell_0_0, next] {
      all_white[range[from[Cell_0_0, next], c1.~next, next]]
      some c2: c1.^next {
        all_black[range[c1, c2, next], 3]
        all_white[c2.^next]
      }
    }
    some c1: from[Cell_0_1, next] {
      all_white[range[from[Cell_0_1, next], c1.~next, next]]
      some c2: c1.^next {
        all_black[range[c1, c2, next], 2]
        some c1: c2.next.^next {
          all_white[range[c2.next.^next, c1.~next, next]]
          some c2: c1.^next {
            all_black[range[c1, c2, next], 2]
            all_white[c2.^next]
          }
        }
      }
    }
(以下略)
||<

まあ、こんな一部省略したコードではわからないかと思うので
こちらに生成したAlloyのコード: https://github.com/nishio/learning_alloy/blob/master/illustlogic.als
こちらに生成するために書いたPythonのコード: https://github.com/nishio/learning_alloy/blob/master/tool/illulogic.py
を置くなどした。

結果はまだ出ていない。SATソルバに食わせるためのCNFの生成に時間が掛かっている。明日の朝起きて結果が出てたら追記する。

** 追記

約1時間で
>||
Executing "Run run$1 for 5 int"
   Solver=minisatprover(jni) Bitwidth=5 MaxSeq=4 SkolemDepth=1 Symmetry=20
   Generating CNF...

An error has occurred!
||<
異常終了していたｗ

** 追記
JNIでMiniSatを呼び出すのを辞めてSAT4Jにしてみた。メモリ割り当ては最大限まで引き上げた。
>||
Executing "Run run$1 for 5 int"
   Solver=sat4j Bitwidth=5 MaxSeq=4 SkolemDepth=1 Symmetry=20
   Generating CNF...

Fatal Error: the solver ran out of memory!
Try simplifying your model or reducing the scope,
or increase memory under the Options menu.

An error has occurred!
||<
それでも駄目か…

何か他にいじるべき設定があるのかな。それとも現状のsomeが6重ネストしているようなコードがダメなのかな。1ブロックごとにsomeを2個使ってブロックのサイズと数値を比較するんじゃなくて.next.next.nextみたいな書き方のコードを出力してsomeを半分に減らすとなんとかなるかな？？

** まとめ
「イラストロジックはAlloyに向かない」

って書いておくと id:bonotake さんが解決してくれないかな(チラッ) cf. <a href='http://d.hatena.ne.jp/bonotake/20110829/1314605061'>カロタンパズルをAlloyで解く - たけをの日記＠天竺から帰ってきたよ</a>

** 追記

といてもらえました！ありがとうございます！ <a href='http://d.hatena.ne.jp/bonotake/20120413/1334300370'>イラストロジックをAlloyで解く - たけをの日記＠天竺から帰ってきたよ</a>

このネタでAlloyガール番外編を書きました！ <a href='http://d.hatena.ne.jp/nishiohirokazu/20120413/1334305940'>Alloyガール番外編「イラストロジック」</a>
</body>
<comments>
<comment>
<username>bonotake</username>
<body>自分の日記に解けたものを載せときましたが、こちらにもちょびっとコメントしておきますね。<br>CNFの生成でコケてる場合、基本的にソルバの選択は関係ないです。JNI叩く前にコケてますので。<br>ただし、minisatprover （MiniSat with Unsat Core）だけは別で、これはCNF生成にかなり時間を食います。coreが出てくると便利なんですが、大きな問題では避けたほうが無難です。</body>
<timestamp>1334306784</timestamp>
</comment>
</comments>
```


[はてなダイアリー 2012-04-11](https://nishiohirokazu.hatenadiary.org/archive/2012/04/11)