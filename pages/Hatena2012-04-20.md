
hatena

```
<body>
*1334857551*Alloyで言語女子会殺人事件
事件: PHPが殺されました

** 状況設定

PHP, Python, Ruby, Perlは仲良し四人組。PHPが殺された当日もその4人が一緒にいるのが目撃されている。しかしなんか諸般の事情で全員に殺害動機はあるらしい。Perlは友人が殺されたショックで寝込んでいるのでまずはRubyとPythonに事情聴取することにした。警部はAlloy。Alloyについては<a href='http://d.hatena.ne.jp/nishiohirokazu/20120410/1334024972'>Alloyまとめ</a>を参照。

** Rubyを事情聴取

Ruby: 犯人は私ではないわ
Alloy: 私はそんなことは主張していないわ。
Ruby: 犯人はPythonよ。
Alloy: どうしてそう思うの？
Ruby: 最初、私とPython、PHPの3人で裏山に行く予定だったのよ。でも私は家の都合で買い物に行かなきゃいけなくなったの。Perlはなんか採集するとかで海岸に行くって言ってたからPHPと一緒にいたのはPythonだけよ。

** Pythonを事情聴取

Python: 犯人は私ではないわ
Alloy: 私はそんなことは主張していないわ。
Python: 犯人はRubyよ。
Alloy: どうしてそう思うの？
Python: だって私達が裏山に行くって聞いた後、PHPは一人で買い物に行くって言ってたもの。Rubyがその後で私とPHPに買い物に行くって言ったから犯行当時PHPと一緒にいたのはRubyよ。Perlは真珠を探すとか言って海岸に行ったからRubyにしか犯行はできないわ。

** 問題
Alloy: PythonとRubyがお互いに相手を犯人だと思う事例を探せ、って言われて私が考えたのがこういうシチュエーションなんだけど。
PHP: なんで私が殺され役…
Alloy: 誰が犯人だと思う？
JS: えー、RubyとPythonとどっちかが嘘を付いているのかなー？
Alloy: どちらも嘘はついていないわ。
JS: 犯人がPython、Ruby、Perlの他にいたりする？
Alloy: それもないわ。
JS: じゃあPythonもRubyも自分は犯人じゃないって言ってるんだからPerlが犯人じゃん？
Alloy: …しまった！制約を満たすことばっかり考えてて問題の難易度のことは忘れてた！
JS: 答えを出すのに必要ないけど、誰にも言わずにどこかに行くことはある？
Alloy: ないわ。途中で気が変わることはあるけども、全員最終的に行こうと思ったところを誰かに伝えて、言ったとおりにその場所にいった設定になっているわ。
JS: じゃあ、まだ観測されていないPHPの発言があるわね。まだ観測されていないってことは聞き手はPerl一人ということかな。
Alloy: そうなるわね。
JS: PHPが買い物にも裏山にも言ってないということは、その2つの発言より後に気が変わって「海岸に行く」とPerlに伝えているはずね。

** 解決編
Alloy: あなたがPHPに「買い物に行く」って聞いた時、その場にRubyはいたの？
Python: え、うーん、そういえば職員室に呼ばれてたんだっけ。Rubyはいなかったわね。
Ruby: そうよ、買い物に行くなんて聞いてないわ。
Alloy: つまり、RubyはPHPがPythonに「買い物に行く」と伝えたことを知らなかったので、先ほどのように答えるのはしかたない。何も嘘はついていない。
Ruby: じゃあPythonが犯人よ！
Python: PHPは私に買い物に行くって言ったし、実際に裏山には来なかったのよ。
Alloy: でも、それはPHPが買い物に行ったことを意味しないわね。
Python: そうか…

** 解答

-1: PHP, Python, Ruby が Perlに裏山へ行くと伝える
-2: Rubyが不在、PHPがPython, Perlに買い物に行くと伝える
-3: PerlはPython, Rubyに海岸へ行くと伝える。
-4: PHPはその後Perlにメールでやっぱ海岸に行くと伝える
-5: RubyはPythonとPHPに買い物に行くと伝える

Rubyは1,3,5しか知らない。Pythonは1,2,3,5を知っている。この知識の違いによってお互いに相手が犯人だと思う事例が発生した。

** まとめ

というわけでミステリーのタネをAlloyで生成できないかに挑戦してみた結果、なんかそれっぽいのはできた。話として面白くするためにはもうちょっとコードに手を加える必要がありそう。

まず「全員正直」って条件が厳しすぎる。「自分が行く場所を他人に必ず伝える」という条件も、とても犯人っぽくない。犯人だけ嘘をつくとかがいいのかもな。

それから、犯人に「自分が犯人だと特定されないようにしよう」という意図が全くない。そもそも「自分が犯人だと特定されない」と思ってすらいない。Perlが犯行に及んだ時点で、Perlは自分が犯人と思われないとか、PythonとRubyがお互いを犯人だと思うであろうとか判断できる根拠を全然持ってない。PerlはRubyとPythonが一緒に裏山に行ってると思ってるからね。

複数の人間の「誤解」や「相互不信」が起きる事例をAlloyで導出できたのは面白かった。今後もっと面白いことができそうな予感。

** コード
今回は第二次誤信念は扱っていないけど「自分に都合がいいように他人が誤解するように嘘の情報を流す」ということを犯人がやるためには他人の信念に対する信念が必要であって、しかも犯人は完璧に成功したつもりでいるけどもミスがあってそこからほころびるわけだから第二次誤信念だよね。

あとソースコードはもうちょっと綺麗にする余地がありそう。

あっ、読者が誤解をすることをモデリングしようとしてObserverを作ったのに結局使ってないや。

関数same_place :: Person -> Person -> Personが割と誤解しそうな名前で、same_place[A, X] = Bは「Aが『Xと一緒にいた』と考える人の集合はB一人だけの集合」って意味になっている。

https://github.com/nishio/learning_alloy/blob/master/mardar.als

>||
open util/ordering[Time]

abstract sig AbsPerson {
	//観察者も含めた人物
	belief: Person -> Person -> Place -> Time,
}
sig Observer extends AbsPerson {}

//登場人物
abstract sig Person extends AbsPerson{
	// 最終的にどこへ行ったか
	real_place: Place -> Time
}{
	all t: Time | lone real_place.t
}

one sig X extends Person{}
one sig A extends Person{}
one sig B extends Person{}
one sig C extends Person{}

sig Time {
	// 各時刻に誰かが誰かに自分がどこに行くかを伝える
	who: some Person,
	targets: some AbsPerson,
	where: Place
}{
	// 話し手は聞き手に含まれる
	who in targets
	// 聞き手が話し手と同一ではいけない
	some targets - Observer - who
}

sig Place {}

fact {
	// 同じ人が複数箇所に存在すると信じることはない
	all p1, p2, p3: Person, t: Time{
		lone (p1.belief)[p2, p3].t
	}
}
// whoと同じ場所にいた可能性があるのは？
fun same_place(self: AbsPerson, who: Person): Person{
	let where = self.(self.belief).last {
		who.where.~where + {p: Person | no p.where} - who
	}
}

pred init(t: Time){
	all p: AbsPerson {
		no p.belief.t
	}
	all p: Person {
		no p.real_place.t
	}
}
pred step(t', t: Time){
	all p: Person {
		(p in t.targets) => {
			// 聞いた人の信念が上書きされる
			p.belief[p].t = p.belief[p].t' ++ (t.who -> t.where)
			// 第二次誤信念は今回は扱わない
			no p.belief[Person - p].t
		}else{
			p.belief.t = p.belief.t'
		}
		(p in t.who) => {
			// 自分が言ったところに行く(嘘はつかない)
			p.real_place.t = t.where
		}else{
			p.real_place.t = p.real_place.t'
		}
	}
}
pred final(t', t: Time){
	all p: Person {
		let go = real_place.t',
				place = p.go, 
				meets = go.place {
			// 最終的にどこかには行った
			one place
			p.real_place.last = place
			// 最終的に同じ場所に行った人は信念が更新される
			p.belief[p].t = p.belief[p].t' ++ (meets -> place)
		}
	}
}
run {
	init[first]
	all t: Time - last - first{
		step[t.prev, t]
	}
	final[last.prev, last]
	// AはBが犯人だと、BはAが犯人だと考えている
	same_place[A, X] = B
	same_place[B, X] = A
	// 二人ともアリバイはないが自分が犯人ではない
	no same_place[A, A]
	no same_place[B, B]
	// Cは自分が犯人だと知っている
	same_place[C, X] = C
} for 7 Time, 3 Place, 1 Observer
||<

*1334902176*Alloyでピーターソンのアルゴリズム
>>
ピーターソンのアルゴリズムは、通信のために共有メモリだけを使い「2個」のプロセス間でリソースを競合することなく共有する相互排他のためのアルゴリズムである。
<<
<a href='http://ja.wikipedia.org/wiki/%E3%83%94%E3%83%BC%E3%82%BF%E3%83%BC%E3%82%BD%E3%83%B3%E3%81%AE%E3%82%A2%E3%83%AB%E3%82%B4%E3%83%AA%E3%82%BA%E3%83%A0'>ピーターソンのアルゴリズム - Wikipedia</a>

Alloyでモデリングしてみた。 https://github.com/nishio/learning_alloy/blob/master/Peterson.als

Wikipediaに書いてある「P0とP1は決して同時にクリティカルセクションには入らない。」を検証してみよう。for 15 Timeのスコープで2つのプロセスのプログラムカウンタが同時に3(つまりクリティカルセクション内)にならないことを検証させる。反例は見つからない。

>||
check MutualExclusion {
	no t: Time {
		PC.proc[0].t = 3
		PC.proc[1].t = 3
	}
} for 15 Time
||<

じゃあ「プロセスはクリティカルセクション1回分の処理時間以上に待たされることはない」も検証してみよう。やってみたらさくっと反例が見つかった。

>||
check BoundedWaiting_Bad {
	no t: Time, pid: Int {
		PC.proc[pid].t = 2
		PC.proc[pid].(t.next) = 2
		PC.proc[pid].(t.next.next) = 2
		PC.proc[pid].(t.next.next.next) = 2
		PC.proc[pid].(t.next.next.next.next) = 2
	}
} for 15 Time
||<

理由は割と簡単で「各時刻にどちらかのプロセスが1ステップ実行される」という仕様では「自分は『進める状態』相手が『待たされる状態』で、相手プロセスが5回連続で実行される」が起こりうるから。「各時刻1個につき、ランダムに選ばれた片方のプロセスが1ステップ実行される」という条件ではランダムに選ばれた結果 「待つべき側のプロセスばかり連続して実行される(その結果何も変わらない)」が起こるわけだ。

だからWikipediaの 「プロセスはクリティカルセクション1回分の処理時間以上に待たされることはない」 を文字通りに捉えたのが間違い。たとえば「あるプロセスが待っている間に片方のプロセスが2回連続してクリティカルセクションを実行することはない」とするべき。よし、これならfor 15 Timeのスコープで反例はない。

>||
check BoundedWaiting {
	no t, t': Time, pid: Int {
		let range = Time - t.prevs - t'.nexts {
			// あるプロセスがずっと2(ロック待ち)で
			all t'': range | PC.proc[pid].t'' = 2
			// その間、もう片方のプロセスが2回以上3(クリティカルセクション)を実行
			#{t'': range | 
				PC.proc[PC.current_pid.t''].t'' = 3
			} >= 2
		}
	}
} for 15 Time
||<

** アウトオブオーダー実行

さてさて、ここからが本題なのだが、ピーターソンのアルゴリズムは命令が書かれた順序で実行されることを想定している。現代のCPUはそうではない((<a href='http://ja.wikipedia.org/wiki/%E3%82%A2%E3%82%A6%E3%83%88%E3%83%BB%E3%82%AA%E3%83%96%E3%83%BB%E3%82%AA%E3%83%BC%E3%83%80%E3%83%BC%E5%AE%9F%E8%A1%8C'>アウト・オブ・オーダー実行 - Wikipedia</a>))。だから現代のCPUで動くようにするには適切にフェンスを置く必要がある((<a href='http://ja.wikipedia.org/wiki/%E3%83%A1%E3%83%A2%E3%83%AA%E3%83%90%E3%83%AA%E3%82%A2'>メモリバリア - Wikipedia</a>))。

で、光成さん((<a href='http://homepage1.nifty.com/herumi/'>With Unz</a>))に色々教えてもらいながらgcc -S -O3 -fomit-frame-pointer tmp.cしたり#include <emmintrin.h>して_mm_mfence();したり「<a href='http://d.hatena.ne.jp/bsdhouse/20101016/1287227939'>ピーターソンのアルゴリズムとメモリバリア - yamasaのネタ帳</a>のコードのlockの中のwhileの前にmfenceをおいたら僕のMacBook AirのCore 2 Duoでは問題なく動くようになった。sfenceでも動くけどlfenceだと動かない。なるほど！と思ったら別のCPUだと逆にsfenceが動かなくてlfenceが動くぞ！これはたまたま動いているだけか！これじゃ実機で実行してみて問題が見つからなくても全然安心できないじゃないか！」と騒いだりした。

というわけで実機で試しても何も安心できないので、今後Alloyでアウトオブオーダー実行をモデリングしようと思うんだけど、とりあえず手始めに0行目と1行目のstoreが入れ替わったらどうなるか試してみたらさくっと2.5秒でクリティカルセクションに両方が入る反例が見つかった。
>||
Time$1: pc0=0, pc1=1, pid=1 turn=0 f0=0 f1=0 // p1がturnを0にする 
Time$2: pc0=1, pc1=1, pid=0 turn=1 f0=0 f1=0 // p0がturnを1にする 
Time$3: pc0=2, pc1=1, pid=0 turn=1 f0=1 f1=0 // p0がf1==0だから「p1は待っていない」と判断、進入 
Time$4: pc0=3, pc1=1, pid=0 turn=1 f0=1 f1=0 
Time$5: pc0=3, pc1=2, pid=1 turn=1 f0=1 f1=1 // p1が「turnが1だから俺が進んでよい」と判断、進入 
Time$6: pc0=3, pc1=3, pid=1 turn=1 f0=1 f1=1 // ここで両方ともクリティカルセクション(pc=3)にいる 
Time$7: pc0=3, pc1=0, pid=1 turn=1 f0=1 f1=0 
Time$8: pc0=0, pc1=0, pid=0 turn=1 f0=0 f1=0 
Time$9: pc0=1, pc1=0, pid=0 turn=1 f0=0 f1=0
||<
だから0行目と1行目の間には最低限sfenceは必要なわけだな。

追記: 「sfenceが必要」と書いたのは言いたかったことを正確に表現できていないということがわかった。see <a href='http://homepage1.nifty.com/herumi/diary/1204.html#21'>melancholic afternoon</a>。「アウトオブオーダー実行でW→Wの順序を入れ替える可能性のあるCPUでピーターソンのアルゴリズムを動かす場合、0行目と1行目の間にはW→Wの順序を保つ制約を入れる必要がある」と言うべきだった。Core 2 Duo, Atom, Core Duo, Pentium 4, P6ではW→Wの順序は(一部の命令を除いて)保存されるのでsfenceを入れる必要もないようだ。

次回予定: 「Alloyでアウトオブオーダー実行」
</body>
```


[はてなダイアリー 2012-04-20](https://nishiohirokazu.hatenadiary.org/archive/2012/04/20)